# Copyright 2015 The Fuchsia Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

import("//apps/mozart/config.gni")

group("skia") {
  public_deps = [
    ":fonts",
    ":type_converters",
    ":vmo",
  ]
}

source_set("fonts") {
  sources = [
    "skia_font_loader.cc",
    "skia_font_loader.h",
  ]

  deps = [
    ":type_converters",
    ":vmo",
    "//apps/fonts/services",
    "//lib/ftl",
    "//third_party/skia",
  ]
}

source_set("type_converters") {
  sources = [
    "type_converters.cc",
    "type_converters.h",
  ]

  public_deps = [
    "//apps/mozart/services/geometry",
    "//third_party/skia",
  ]
}

source_set("vmo") {
  sources = [
    "skia_vmo_data.cc",
    "skia_vmo_data.h",
    "skia_vmo_image.cc",
    "skia_vmo_image.h",
    "skia_vmo_surface.cc",
    "skia_vmo_surface.h",
  ]

  public_deps = [
    "//apps/mozart/services/buffers",
    "//apps/mozart/services/buffers/cpp",
    "//apps/mozart/services/composition",
    "//lib/ftl",
    "//lib/mtl",
    "//magenta/system/ulib/mx",
    "//third_party/skia",
  ]

  configs += [ "//apps/mozart:vulkan_config" ]

  if (mozart_use_vulkan) {
    sources += [
      "vk_vmo_image_generator.cc",
      "vk_vmo_image_generator.h",
    ]
    public_deps += [
      "//flutter/vulkan",
      "//magma:vulkan",
      "//third_party/skia:gpu",
    ]
  }
}
